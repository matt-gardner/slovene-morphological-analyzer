LEXICON Numeral
LEXICON NumDigCard

+Numeral+Digit+Cardinal: #;

LEXICON NumDigOrd

+Numeral+Digit+Ordinal: #;

LEXICON NumRomCard

+Numeral+Roman+Cardinal: #;

LEXICON NumRomOrd

+Numeral+Roman+Ordinal: #;

LEXICON NumCardInf

+Numeral+Letter+Cardinal: CardinalEndings;

LEXICON Numeral

! Sadly, these two need to be done here instead of in numerals_overrides.lexc,
! if I want to make use of the mechanisms for replacing fleeting vowels.
! Either it's one line each here (with a line in create_lexica.py to remove
! them from numerals.lexc), or six lines each in numerals_overrides.lexc.
osem+Numeral+Letter+Cardinal:osem@P.FLEETING.REPLACE@ CardinalEndings;
sedem+Numeral+Letter+Cardinal:sedem@P.FLEETING.REPLACE@ CardinalEndings;

LEXICON CardinalEndings

! These are like adjectives, but different enough (e.g., no gender, only
! plural), to merit separate treatment here

+NoGender+Plural+Nom: #;
+NoGender+Plural+Gen:^ih #;
+NoGender+Plural+Dat:^im #;
+NoGender+Plural+Acc: #;
+NoGender+Plural+Loc:^ih #;
+NoGender+Plural+Ins:^imi #;

LEXICON NumCardIndecInf

+Numeral+Letter+Cardinal: IndeclinableCardinalEndings;

LEXICON IndeclinableCardinalEndings

+NoGender+Plural+Nom: #;
+NoGender+Plural+Gen: #;
+NoGender+Plural+Dat: #;
+NoGender+Plural+Acc: #;
+NoGender+Plural+Loc: #;
+NoGender+Plural+Ins: #;
+NoGender+Plural+Gen:^tih #;
+NoGender+Plural+Dat:^tim #;
+NoGender+Plural+Loc:^tih #;
+NoGender+Plural+Ins:^timi #;

LEXICON NumOrdInf

+Numeral+Letter+Ordinal:@P.ADJ_REMOVE_I.Y@ AdjNoDefiniteness;

LEXICON Numeral

eden+Numeral+Letter+Pronominal+Masc+Sing+Nom:eden #;
en+Numeral+Letter+Pronominal:en AdjNoDual;
drug+Numeral+Letter+Pronominal:drug AdjNormalDefiniteness;

LEXICON NumSpecInf

+Numeral+Letter+Special: AdjInfBase;
! This seems rather odd to me; I wonder if there's a problem with the MSDs...
+Numeral+Letter+Special+Masc+Sing+Acc: #;
+Numeral+Letter+Special+Masc+Sing+Nom+Indefinite: #;

LEXICON NumSpecEnInf

+Numeral+Letter+Special:@P.FLEETING.REPLACE@ AdjNormalDefiniteness;

LEXICON AdjNoDual

+Masc+Sing+Nom: #;
+Masc+Sing+Acc: #;
+Masc+Sing+Gen:^ega #;
+Masc+Sing+Dat:^emu #;
+Masc+Sing+Acc:^ega #;
+Masc+Sing+Loc:^em #;
+Masc+Sing+Ins:^im #;
+Masc+Plural+Nom:^i #;
+Masc+Plural+Gen:^ih #;
+Masc+Plural+Dat:^im #;
+Masc+Plural+Acc:^e #;
+Masc+Plural+Loc:^ih #;
+Masc+Plural+Ins:^imi #;

+Fem+Sing+Nom:^a #;
+Fem+Sing+Gen:^e #;
+Fem+Sing+Dat:^i #;
+Fem+Sing+Acc:^o #;
+Fem+Sing+Loc:^i #;
+Fem+Sing+Ins:^o #;
+Fem+Plural+Nom:^e #;
+Fem+Plural+Gen:^ih #;
+Fem+Plural+Dat:^im #;
+Fem+Plural+Acc:^e #;
+Fem+Plural+Loc:^ih #;
+Fem+Plural+Ins:^imi #;

! The @P.TYPE.NEUTADJ@ is for changing o to e after certain consonants.  I
! could put it everywhere, but it only matters on those two cases.
+Neut+Sing+Nom:@P.TYPE.NEUTADJ@^o #;
+Neut+Sing+Gen:^ega #;
+Neut+Sing+Dat:^emu #;
+Neut+Sing+Acc:@P.TYPE.NEUTADJ@^o #;
+Neut+Sing+Loc:^em #;
+Neut+Sing+Ins:^im #;
+Neut+Plural+Nom:^a #;
+Neut+Plural+Gen:^ih #;
+Neut+Plural+Dat:^im #;
+Neut+Plural+Acc:^a #;
+Neut+Plural+Loc:^ih #;
+Neut+Plural+Ins:^imi #;
