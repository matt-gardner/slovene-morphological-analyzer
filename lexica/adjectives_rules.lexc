LEXICON Adj
! My grammar book says that only nominative singular adjectives specify for
! definiteness, but the JOS MSDs say that accusative singular adjectives do
! too, so we'll go with theirs, as they are natives.

LEXICON AdjInfI

! It seems to me that this should be unspecified for definiteness, but the JOS
! MSDs give it as +Definite, so that's what I'm doing.
@D.SUP@+A+General+Positive+Masc+Sing+Nom+Definite:^@D.SUP@ #;
@D.SUP@+A+General+Positive+Masc+Sing+Acc+Definite:^@D.SUP@ #;
@D.SUP@+A+General+Positive:@P.ADJ_REMOVE_I.Y@@D.SUP@ AdjInfBase;

LEXICON AdjInf

! The +A on the right means to check for fleeting e
@D.SUP@+A+General+Positive:@P.FLEETING.REPLACE@@D.SUP@ AdjNormalDefiniteness;
@D.SUP@+A+General+Comparative:@P.FLEETING.REPLACE@ejš@D.SUP@ AdjNormalDefiniteness;
@R.SUP@+A+General+Superlative:@P.FLEETING.REPLACE@ejš@R.SUP@ AdjNormalDefiniteness;

LEXICON AdjNoFleetingInf

! No +A on the right here, as these don't have fleeting e's
@D.SUP@+A+General+Positive:@D.SUP@ AdjNormalDefiniteness;
@D.SUP@+A+General+Comparative:ejš@D.SUP@ AdjNormalDefiniteness;
@R.SUP@+A+General+Superlative:ejš@R.SUP@ AdjNormalDefiniteness;

LEXICON AdjInfPoss

! Possessives and participles don't have fleeting e's, either
@D.SUP@+A+Possessive+Positive:@D.SUP@ AdjOnlyIndefinite;

LEXICON AdjInfPart

@D.SUP@+A+Participle+Positive:@D.SUP@ AdjNormalDefiniteness;

LEXICON AdjNormalDefiniteness

+Masc+Sing+Nom+Indefinite: #;
+Masc+Sing+Nom+Definite:^i #;
+Masc+Sing+Acc+Indefinite: #;
+Masc+Sing+Acc+Definite:^i #;
AdjInfBase;

LEXICON AdjOnlyIndefinite

+Masc+Sing+Nom+Indefinite: #;
+Masc+Sing+Acc+Indefinite: #;
AdjInfBase;

LEXICON AdjNoDefiniteness
! The JOS MSDs seems to say that pronouns can't specify definiteness, and that
! is what this is used for right now.  So there's only one ending for each of
! these two.
+Masc+Sing+Nom: #;
+Masc+Sing+Acc: #;
AdjInfBase;

LEXICON AdjInfBase

!+Masc+Sing+Nom+Indefinite: #; ! handled in subclasses
!+Masc+Sing+Nom+Definite:^i #; ! handled in subclasses
+Masc+Sing+Gen:^ega #;
+Masc+Sing+Dat:^emu #;
!+Masc+Sing+Acc+Indefinite: #; ! handled in subclasses
!+Masc+Sing+Acc+Definite:^i #; ! handled in subclasses
+Masc+Sing+Acc:^ega #;
+Masc+Sing+Loc:^em #;
+Masc+Sing+Ins:^im #;
+Masc+Dual+Nom:^a #;
+Masc+Dual+Gen:^ih #;
+Masc+Dual+Dat:^ima #;
+Masc+Dual+Acc:^a #;
+Masc+Dual+Loc:^ih #;
+Masc+Dual+Ins:^ima #;
+Masc+Plural+Nom:^i #;
+Masc+Plural+Gen:^ih #;
+Masc+Plural+Dat:^im #;
+Masc+Plural+Acc:^e #;
+Masc+Plural+Loc:^ih #;
+Masc+Plural+Ins:^imi #;

+Fem+Sing+Nom:^a #;
+Fem+Sing+Gen:^e #;
+Fem+Sing+Dat:^i #;
+Fem+Sing+Acc:^o #;
+Fem+Sing+Loc:^i #;
+Fem+Sing+Ins:^o #;
+Fem+Dual+Nom:^i #;
+Fem+Dual+Gen:^ih #;
+Fem+Dual+Dat:^ima #;
+Fem+Dual+Acc:^i #;
+Fem+Dual+Loc:^ih #;
+Fem+Dual+Ins:^ima #;
+Fem+Plural+Nom:^e #;
+Fem+Plural+Gen:^ih #;
+Fem+Plural+Dat:^im #;
+Fem+Plural+Acc:^e #;
+Fem+Plural+Loc:^ih #;
+Fem+Plural+Ins:^imi #;

! The @P.TYPE.NEUTADJ@ is for changing o to e after certain consonants.  I
! could put it everywhere, but it only matters on those two cases.
+Neut+Sing+Nom:@P.TYPE.NEUTADJ@^o #;
+Neut+Sing+Gen:^ega #;
+Neut+Sing+Dat:^emu #;
+Neut+Sing+Acc:@P.TYPE.NEUTADJ@^o #;
+Neut+Sing+Loc:^em #;
+Neut+Sing+Ins:^im #;
+Neut+Dual+Nom:^i #;
+Neut+Dual+Gen:^ih #;
+Neut+Dual+Dat:^ima #;
+Neut+Dual+Acc:^i #;
+Neut+Dual+Loc:^ih #;
+Neut+Dual+Ins:^ima #;
+Neut+Plural+Nom:^a #;
+Neut+Plural+Gen:^ih #;
+Neut+Plural+Dat:^im #;
+Neut+Plural+Acc:^a #;
+Neut+Plural+Loc:^ih #;
+Neut+Plural+Ins:^imi #;
