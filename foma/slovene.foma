read lexc lexicon.lexc
define Lexicon

define Flags ["@D.SUP@" |
              "@R.SUP@" |
              "@P.SUP.POS@" |
              "@P.FLEETING.REPLACE@" |
              "@P.TYPE.MASCNOUN@" |
              "@P.TYPE.NEUTADJ@"];

define ConsonantForFleeting [c | k | l | m | n | r |  n j];

define FemNounStemming a "+Fem" -> 0 || _ "^" ,,
                       e v "+Fem" -> 0 || _ "^";

define FleetingE e -> 0 || _ ConsonantForFleeting
                    Flags* "@P.FLEETING.REPLACE@" (e j š) Flags* "^";

define Adjectives i "+AdjRemoveI" -> 0;

define Adverbs o "+Adverb" -> 0;

define MascNouns [..] -> j || [a r | o r | u r | i r | e r] _
                            Flags* "@P.TYPE.MASCNOUN@" Flags* "^" .o.
                 o -> e || [c | č | š | ž | j]
                            Flags*
                            ["@P.TYPE.MASCNOUN@" | "@P.TYPE.NEUTADJ@"]
                            Flags* "^" _ .o.
                 "+Masc" -> 0;

define NeutNounStemming o "+Neut" -> 0 || _ "^" ,,
                        e "+Neut" -> 0 || _ "^" .o.
                        "+Neut" -> 0;

define PronounStemming [i | a] "+StemPronoun" -> 0;

define VerbStemming t i "+V" -> 0 .o.
                    n i -> n e || _ "+Present" "^" ,,
                    e -> i "+StemChanged" || _ "+Present" "^" ,,
                    [o v a | e v a] -> u j e || _ "+Present" "^" ,,
                    [o v a | e v a] -> u j || _ "+Imperative" "^" .o.
                    j -> 0 || i "+Imperative" "^" _ .o.
                    "+Present" -> 0 ,,
                    "+StemChanged" -> 0 ,,
                    "+Imperative" -> 0;

define Cleanup "^" -> 0 .o.
               "+ReplaceFleeting" -> 0;

define Grammar Lexicon .o.
               PronounStemming .o.
               FemNounStemming .o.
               Adjectives .o.
               Adverbs .o.
               FleetingE .o.
               MascNouns .o.
               NeutNounStemming .o.
               VerbStemming .o.
               Cleanup;

read lexc overrides.lexc
define Overrides


regex Overrides .P. Grammar;
save stack slovene.bin
exit;
