read lexc lexicon.lexc
define Lexicon

define Flags ["@D.SUP@" | "@R.SUP@" | "@P.SUP.POS@"];
define ConsonantForFleeting [c | k | l | m | n | r |  n j];

define FemNounStemming a "+Fem" -> 0 || _ "^" ,,
                       e v "+Fem" -> 0 || _ "^";

define Adjectives e -> 0 || _ ConsonantForFleeting "+A" ,,
                  i "+AdjRemoveI" -> 0  .o.
                  "+A" -> 0;

define Adverbs o -> 0 || _ "+Adverb" .o.
                  "+Adverb" -> 0;

define MascNouns e -> 0 || _ ConsonantForFleeting "+Masc" "^" .o.
                 [..] -> j || a r | o r | u r | i r | e r _ "+Masc" "^" .o.
                 o -> e || [c | č | š | ž | j] (Flags) "+Masc" "^" _ .o.
                 "+Masc" -> 0;

define NeutNounStemming o "+Neut" -> 0 || _ "^" ,,
                        e "+Neut" -> 0 || _ "^" .o.
                        "+Neut" -> 0;

define VerbStemming t i "+V" -> 0 .o.
                    n i -> n e || _ "+Present" "^" ,,
                    e -> i "+StemChanged" || _ "+Present" "^" ,,
                    [o v a | e v a] -> u j e || _ "+Present" "^" ,,
                    [o v a | e v a] -> u j || _ "+Imperative" "^" .o.
                    "+Present" -> 0 ,,
                    "+StemChanged" -> 0 ,,
                    "+Imperative" -> 0;

define Cleanup "^" -> 0;

define Grammar Lexicon .o.
               FemNounStemming .o.
               Adjectives .o.
               Adverbs .o.
               MascNouns .o.
               NeutNounStemming .o.
               VerbStemming .o.
               Cleanup;
regex Grammar;
save stack slovene.bin
exit;
