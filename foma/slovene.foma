read lexc lexicon.lexc
define Lexicon

define Flags ["@D.SUP@" |
              "@R.SUP@" |
              "@P.SUP.POS@" |
              "@P.FLEETING.REPLACE@" |
              "@P.STEM_PRONOUN.Y@" |
              "@P.STEM_BOUND_PRONOUN.Y@" |
              "@P.ADD_HYPHEN.Y@" |
              "@P.ANIMATE.Y@" |
              "@R.ANIMATE.Y@" |
              "@D.ANIMATE.Y@" |
              "@P.ADD_J.N@" |
              "@P.ADJ_REMOVE_I.Y@" |
              "@P.PROPER.Y@" |
              "@P.NiToNe.N@" |
              "@P.IMPERATIVE_E_TO_I.Y@" |
              "@P.EToI.N@" |
              "@P.TYPE.MASCNOUN@" |
              "@P.TYPE.FEMNOUN@" |
              "@P.TYPE.NEUTNOUN@" |
              "@P.TYPE.NEUTADJ@"
             ];

define ConsonantForFleeting [c | k | l | m | n | r |  n j | l j];

define FemNounStemming a | e v -> 0 || _ Flags* "@P.TYPE.FEMNOUN@" Flags* "^";

define AddHyphen [..] -> "-" j || \Flags _ Flags* "@P.ADD_HYPHEN.Y@" Flags* "^";

define MascNounStemming [a | o] -> 0 || _ Flags* "@P.TYPE.MASCNOUN@" Flags* "^";

define FleetingE e -> 0 || _ ConsonantForFleeting
                    Flags* "@P.FLEETING.REPLACE@" (e j š) Flags* "^";

define Adjectives i -> 0 || \Flags* _ Flags* "@P.ADJ_REMOVE_I.Y@" Flags* "^";

define Adverbs o "+Adverb" -> 0;

define AddJ [..] -> j || [a r | o r | u r | i r | e r | a | o | e | i | u]
                            _ [Flags - "@P.ADD_J.N@"]* "@P.TYPE.MASCNOUN@"
                            [Flags - "@P.ADD_J.N@"]* "^";

define OToE o -> e || [c | č | š | ž | j]
            Flags* ["@P.TYPE.MASCNOUN@" | "@P.TYPE.NEUTADJ@"] Flags* "^" _;

define NeutNounStemming o | e -> 0 || _
                            Flags* "@P.TYPE.NEUTNOUN@" Flags* ["^" | "$"] .o.
                        [..] -> i || \n _ j
                            Flags* "@P.TYPE.NEUTNOUN@" Flags* "$" ,,
                        [..] -> e || \i _ v
                            Flags* "@P.TYPE.NEUTNOUN@" Flags* "$";

define PronounStemming [i | a] -> 0 || \Flags _ Flags* "@P.STEM_PRONOUN.Y@"
                                        Flags* "^" .o.
                       e -> 0 || š _ n Flags* "^" .o.
                       m e -> 0 || \Flags _ Flags* "@P.STEM_BOUND_PRONOUN.Y@"
                                    Flags* "^" .o.
                       [..] -> e || \[a | o] _ Flags* "@P.STEM_BOUND_PRONOUN.Y@"
                                    Flags* "^" n j "$";

define VerbStemming t i -> 0 || _ Flags* "+V" .o.
                    "+V" -> 0 .o.
                    n i -> n e "+StemChanged" ||
                            _ [Flags - "@P.NiToNe.N@"]* "+Present" "^" ,,
                    e -> i "+StemChanged" || _ [Flags - "@P.EToI.N@"]*
                            ["+Present"|"+Imperative"] "^" ,,
                    e -> i "+StemChanged" || _ Flags* "@P.IMPERATIVE_E_TO_I.Y@"
                            Flags* "+Imperative" "^" ,,
                    [o v a | e v a] -> u j e || _ Flags* "+Present" "^" ,,
                    [o v a | e v a] -> u j || _ Flags* "+Imperative" "^" .o.
                    j -> 0 || [i | j]
                            [Flags | "+StemChanged"]* "+Imperative" "^" _ .o.
                    "+Present" -> 0 ,,
                    "+StemChanged" -> 0 ,,
                    "+Imperative" -> 0;

define Cleanup "^" -> 0 .o. "$" -> 0 .o.
               "+ReplaceFleeting" -> 0;

define Grammar Lexicon .o.
               PronounStemming .o.
               FemNounStemming .o.
               AddHyphen .o.
               Adjectives .o.
               Adverbs .o.
               FleetingE .o.
               AddJ .o.
               MascNounStemming .o.
               NeutNounStemming .o.
               OToE .o.
               VerbStemming;

read lexc overrides.lexc
define Overrides

regex Overrides .P. Grammar .o. Cleanup;
save stack slovene.bin
exit;
