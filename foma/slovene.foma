define Vowel [a | e | i | o | u | y];
define UVowel [A | E | I | O | U | Y];
! some of these don't occur in Slovene words, but that's ok
define Cons [b | c | č | d | f | g | h | j | k | l | m | n | p | q | r | s | š
        | t | v | w | x | z | ž];
define UCons [B | C | Č | D | F | G | H | J | K | L | M | N | P | Q | R | S | š
        | T | V | W | X | Z | Ž];
define Digit [%0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9];
define Lower [Vowel | Cons];
define Upper [UVowel | UCons];

define GuessNumber Digit+ ["+Guess":0];
define GuessProperAbbr Upper+ ["+Guess":"@P.ADD_HYPHEN.Y@"];
! this severely overgenerates, but I'm not sure how to make it any better
define GuessProper Upper Lower+ ["+Guess":0];
! this is a better model for how to continue making guessers, but it requires a
! more restricted set of words than just 'proper nouns'
define GuessOst Lower+ o s t ["+Guess":0];

read lexc lexicon.lexc
substitute defined GuessNumber for "+GuessNumber"
substitute defined GuessProperAbbr for "+GuessProperAbbr"
substitute defined GuessProper for "+GuessProper"
substitute defined GuessOst for "+GuessOst"
define Lexicon


define Flags ["@D.SUP@" |
              "@R.SUP@" |
              "@P.SUP.POS@" |
              "@P.FLEETING.REPLACE@" |
              "@P.STEM_PRONOUN.Y@" |
              "@P.STEM_BOUND_PRONOUN.Y@" |
              "@P.ADD_HYPHEN.Y@" |
              "@P.ANIMATE.Y@" |
              "@R.ANIMATE.Y@" |
              "@D.ANIMATE.Y@" |
              "@P.ADD_J.N@" |
              "@P.ADJ_REMOVE_I.Y@" |
              "@P.PROPER.Y@" |
              "@P.NiToNe.N@" |
              "@P.IMPERATIVE_E_TO_I.Y@" |
              "@P.EToI.N@" |
              "@P.TYPE.MASCNOUN@" |
              "@P.TYPE.FEMNOUN@" |
              "@P.TYPE.NEUTNOUN@" |
              "@P.TYPE.NEUTADJ@" |
              "@P.IMPERATIVE.Y@" |
              "@D.IMPERATIVE@"
             ];

define ConsonantForFleeting [c | k | l | m | n | r |  n j | l j];

define FemNounStemming a | e v | e -> 0 ||
                            _ Flags* "@P.TYPE.FEMNOUN@" Flags* ["^" | "$"] .o.
                       [..] -> e || \[a | e | i | o | u] _ [v | r | l | l j]
                            Flags* "@P.TYPE.FEMNOUN@" Flags* "$";

define AddHyphen [..] -> "-" j || \Flags _ Flags* "@P.ADD_HYPHEN.Y@" Flags* "^";

define MascNounStemming Vowel -> 0 ||
                                _ Flags* "@P.TYPE.MASCNOUN@" Flags* "^";

define FleetingE e -> 0 || _ ConsonantForFleeting
                    Flags* "@P.FLEETING.REPLACE@" (e j š) Flags* "^";

define Adjectives i -> 0 || \Flags* _ Flags* "@P.ADJ_REMOVE_I.Y@" Flags* "^";

define Adverbs o "+Adverb" -> 0;

define AddJ [..] -> j || [Vowel r | Vowel]
                            _ [Flags - "@P.ADD_J.N@"]* "@P.TYPE.MASCNOUN@"
                            [Flags - "@P.ADD_J.N@"]* "^";

define OToE o -> e || [c | č | š | ž | j]
            Flags* ["@P.TYPE.MASCNOUN@" | "@P.TYPE.NEUTADJ@"] Flags* "^" _;

define NeutNounStemming o | e -> 0 || _
                            Flags* "@P.TYPE.NEUTNOUN@" Flags* ["^" | "$"] .o.
                        [..] -> i || \n _ j
                            Flags* "@P.TYPE.NEUTNOUN@" Flags* "$" ,,
                        [..] -> e || \i _ v
                            Flags* "@P.TYPE.NEUTNOUN@" Flags* "$";

define PronounStemming [i | a] -> 0 || \Flags _ Flags* "@P.STEM_PRONOUN.Y@"
                                        Flags* "^" .o.
                       e -> 0 || š _ n Flags* "^" .o.
                       m e -> 0 || \Flags _ Flags* "@P.STEM_BOUND_PRONOUN.Y@"
                                    Flags* "^" .o.
                       [..] -> e || \[a | o] _ Flags* "@P.STEM_BOUND_PRONOUN.Y@"
                                    Flags* "^" n j "$";

define VerbStemming t i -> 0 || _ Flags* "+V" .o.
                    "+V" -> 0 .o.
                    n i -> n e "+StemChanged" ||
                            _ [Flags - "@P.NiToNe.N@"]* "+Present" "^" ,,
                    e -> i "+StemChanged" || _ [Flags - "@P.EToI.N@"]*
                            ["+Present"|"@P.IMPERATIVE.Y@"] "^" ,,
                    e -> i "+StemChanged" || _ Flags* "@P.IMPERATIVE_E_TO_I.Y@"
                            Flags* "@P.IMPERATIVE.Y@" "^" ,,
                    [o v a | e v a] -> u j e || _ Flags* "+Present" "^" ,,
                    [o v a | e v a] -> u j ||
                            _ Flags* "@P.IMPERATIVE.Y@" "^" .o.
                    j -> 0 || [i | j] [Flags | "+StemChanged"]*
                            "@P.IMPERATIVE.Y@" "^" _ .o.
                    "+Present" -> 0 ,,
                    "+StemChanged" -> 0;

define Cleanup "^" -> 0 .o. "$" -> 0 .o.
               "+ReplaceFleeting" -> 0;

define Grammar Lexicon .o.
               PronounStemming .o.
               FemNounStemming .o.
               AddHyphen .o.
               Adjectives .o.
               Adverbs .o.
               FleetingE .o.
               AddJ .o.
               MascNounStemming .o.
               NeutNounStemming .o.
               OToE .o.
               VerbStemming;

read lexc overrides.lexc
define Overrides

regex Overrides .P. Grammar .o. Cleanup;
save stack slovene.bin
exit;
